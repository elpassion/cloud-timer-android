import org.ajoberstar.grgit.*
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'io.fabric'
apply plugin: 'jacoco'

def getCrashlyticsBetaMessage (){
    def git = Grgit.open(project.file('..'))
    def commit = git.log(maxCommits: 1)[0]
    def messageFormat = "%s\n  -------------------------------\n%s"
    return sprintf(messageFormat, [commit.author.name, commit.fullMessage])
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "pl.elpassion.cloudtimer"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug{
            testCoverageEnabled true
            ext.betaDistributionReleaseNotes = getCrashlyticsBetaMessage()
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.betaDistributionGroupAliases = 'team'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding{
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude group: 'com.android.support'
    }
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support'
    }
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}
buildscript {
    ext.kotlin_version = '1.0.0-beta-4589'
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.ajoberstar:grgit:1.1.0'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1x'
    }
}
repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}



